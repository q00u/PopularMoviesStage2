apply plugin: 'com.android.application'

//key hiding from https://medium.com/@georgevrynios/android-studio-how-to-prevent-your-sensitive-data-like-api-keys-from-publishing-on-github-repos-fb29af482952
//Read sensitive data from 'sensitive_data.txt' if it exists. Reviewers can add their API key here directly below
def noKeyFound = '"NO_KEY_FOUND"'
def theMovieDbApiKey = noKeyFound

def sensitiveDataFile = file('../sensitive_data.txt')
Properties sensitiveData = new Properties()

if (sensitiveDataFile.canRead()) {
    sensitiveData.load(new FileInputStream(sensitiveDataFile))
    if (null != sensitiveData) {
        if (sensitiveData.containsKey('theMovieDbApiKey')) {
            theMovieDbApiKey = sensitiveData['theMovieDbApiKey']
        }
    }
}

//TODO: ADD YOUR API HERE!
if (noKeyFound == theMovieDbApiKey) {
    theMovieDbApiKey = '"REVIEWERS_THEMOVIEDBAPIKEY_GOES_HERE"'
}

android {
    compileSdkVersion 27
    defaultConfig {
        buildConfigField "String", "API_KEY", theMovieDbApiKey
        resValue "string", "THEMOVIEDB_API_KEY", theMovieDbApiKey
        applicationId "com.example.phoen.popularmovies"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:palette-v7:27.1.1'
    implementation 'com.android.support:exifinterface:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:8.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
